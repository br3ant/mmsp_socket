plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'kotlinx-serialization'
    id 'signing'
}

group = "io.github.br3ant"
version = "1.1.2"

android {
    namespace 'com.br3ant.mmsp_socket'
    compileSdk 33

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = this.group
                artifactId = 'mmsp_socket'
                version = this.version

                pom {
                    name = 'mmsp_socket'
                    description = 'A huisheng mmsp socket library'
                    url = 'https://gitee.com/winsound/mmsp_socket'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://gitee.com/winsound/mmsp_socket/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'br3ant'
                            name = 'br3ant'
                            email = '1106617567@qq.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@gitee.com:winsound/mmsp_socket.git'
                        developerConnection = 'scm:git:ssh://git@gitee.com:winsound/mmsp_socket.git'
                        url = 'https://gitee.com/winsound/mmsp_socket'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "ossrh-staging-api"
                url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                credentials {
                    username = localProperties.getProperty("ossrhUsername")
                    password = localProperties.getProperty("ossrhPassword")
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                localProperties.getProperty("signing.keyId"),
                new String(localProperties.getProperty("signing.key").decodeBase64()),
//                localProperties.getProperty("signing.key"),
                localProperties.getProperty("signing.password")
        )
        sign publishing.publications.release
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.5.0'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0")
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0")


    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}